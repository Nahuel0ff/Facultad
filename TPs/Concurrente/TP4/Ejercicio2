programa ejercicio2
areas
  aveni5: AreaPC (5,1,5,100)
  aveni10: AreaPC (10,1,10,100)
  punto: AreaPC (50,50,50,50)
  areaconsul1: AreaP (11,1,11,1)
  areaconsul2: AreaP (12,1,12,1)
robots
  robot productor
  variables
    papeles:numero
    Ca:numero
    Av:numero
    terminar:boolean
  comenzar 
    papeles:=0
    Av:=PosAv
    terminar:=F
    mientras (~terminar)
      si (HayPapelEnLaEsquina)
        tomarPapel
        papeles:=papeles+1
        si (papeles=5)
          Ca:=PosCa
          BloquearEsquina(50,50)
          Pos(50,50)
          repetir 5
            depositarPapel
          Pos(Av,Ca)
          LiberarEsquina(50,50)
          papeles:=0
      sino
        si (PosCa=100)
          terminar:=V
        sino
          mover
  fin
  robot consumidor
  variables
    papeles:numero
    intentos:numero
    cant:numero
    Av:numero
  comenzar
    Av:=PosAv
    intentos:=0
    papeles:=0
    mientras (intentos<8)
      Random(cant,2,5)
      BloquearEsquina(50,50)
      Pos(50,50)
      mientras ((HayPapelEnLaEsquina)&(papeles<cant))
        tomarPapel
        papeles:=papeles+1
      si (papeles<>cant)
        repetir papeles
          depositarPapel
      Pos(Av,1)
      LiberarEsquina(50,50)
      si (papeles=cant)
        intentos:=0
        repetir papeles
          depositarPapel
      sino 
        intentos:=intentos+1
      papeles:=0
  fin
variables
  consu1: consumidor
  consu2: consumidor
  prod1: productor
  prod2: productor
comenzar
  AsignarArea(consu1, areaconsul1)
  AsignarArea(consu2, areaconsul2)
  AsignarArea(prod1, aveni5)
  AsignarArea(prod2, aveni10)
  AsignarArea(consu1,punto)
  AsignarArea(consu2, punto)
  AsignarArea(prod1, punto)
  AsignarArea(prod2,punto)
  Iniciar(prod1, 5,1)
  Iniciar(prod2, 10,1)
  Iniciar(consu1, 11,1)
  Iniciar(consu2, 12,1)
fin
